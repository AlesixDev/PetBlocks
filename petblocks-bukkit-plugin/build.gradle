import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.2'
    id 'application'
}

apply plugin: 'kotlin-platform-jvm'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
}

setMainClassName("com.github.shynixn.petblocks.bukkit.PetBlocksPlugin")

test {
    // enable JUnit Platform (a.k.a. JUnit 5) support
    useJUnitPlatform()

    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true

    // listen to events in the test execution lifecycle
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }

    // Fail the 'test' task on the first test failure
    failFast = true

    // listen to standard out and standard error of the test JVM(s)
    onOutput { descriptor, event ->
        logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
    }
}

shadowJar{
    archiveName = 'PetBlocks-Bukkit.jar'
    classifier = null
    version = null

    // Change the output folder of the plugin.
    destinationDir = file('D:/Temp/plugins')

    relocate 'kotlin', 'com.github.shynixn.petblocks.lib.kotlin'
    relocate 'org.intellij', 'com.github.shynixn.petblocks.lib.org.intellij'
    relocate 'org.jetbrains', 'com.github.shynixn.petblocks.lib.org.jetbrains'
    relocate 'org.bstats', 'com.github.shynixn.petblocks.lib.org.bstats'
    relocate 'javax.inject', 'com.github.shynixn.petblocks.lib.javax.inject'
    relocate 'org.aopalliance', 'com.github.shynixn.petblocks.lib.org.aopalliance'
    relocate 'org.slf4j', 'com.github.shynixn.petblocks.lib.org.slf4j'
    relocate 'com.google', 'com.github.shynixn.petblocks.lib.com.google'
    relocate 'com.zaxxer', 'com.github.shynixn.petblocks.lib.com.zaxxer'
}

dependencies {
    implementation project(':petblocks-core-java')
    implementation project(':petblocks-bukkit-api')

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.slf4j:slf4j-jdk14:1.7.25'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'com.zaxxer:HikariCP:3.2.0'
    implementation 'com.google.inject:guice:4.1.0'
    implementation 'org.bstats.bStats-Metrics:bstats-bukkit:1.3'

    compileOnly 'com.sk89q:worldguard:6.1'
    compileOnly 'me.minebuilders:clearlag-core:2.9.7'
    compileOnly 'org.spigotmc:spigot113R2:1.13.2-R2.0'

    testCompile 'org.jetbrains.kotlin:kotlin-test'
    testCompile 'org.jetbrains.kotlin:kotlin-test-junit'
    testCompile 'org.mockito:mockito-core:2.23.0'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.1'
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.1'
    testCompile 'org.xerial:sqlite-jdbc:3.23.1'
    testCompile 'ch.vorburger.mariaDB4j:mariaDB4j:2.2.3'
    testCompile 'org.spigotmc:spigot18R1:1.8.0-R1.0'
}