import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'kotlin-multiplatform' version '1.3.0'
    id 'com.github.johnrengelman.shadow' version '4.0.2'
    id 'application'
}
repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
}

group 'com.github.shynixn'
version '8.0.0-SNAPSHOT'
mainClassName = 'PetBlocks'

kotlin {
    targets {
        fromPreset(presets.jvm, 'petblocksBukkitApi')
        fromPreset(presets.jvm, 'petblocksBukkitPlugin')
        fromPreset(presets.jvm, 'petblocksCoreJava')
    }
    sourceSets {
        petblocksApiMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
            }
        }
        petblocksBukkitApiMain {
            dependencies {
                dependsOn petblocksApiMain
                dependsOn petblocksCoreJavaMain

                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
                compileOnly 'org.spigotmc:spigot113R2:1.13.2-R2.0'
            }
        }
        petblocksBukkitPluginMain {
            dependencies {
                dependsOn petblocksBukkitApiMain
                dependsOn petblocksCoreJavaMain

                implementation 'org.slf4j:slf4j-jdk14:1.7.25'
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
                implementation 'com.zaxxer:HikariCP:3.2.0'
                implementation 'com.google.inject:guice:4.1.0'
                implementation 'org.bstats.bStats-Metrics:bstats-bukkit:1.3'

                compileOnly 'com.sk89q:worldguard:6.1'
                compileOnly 'me.minebuilders:clearlag-core:2.9.7'

                compileOnly 'org.spigotmc:spigot18R1:1.8.0-R1.0'
            }
        }
        petblocksCoreMain {
            dependencies {
                dependsOn petblocksApiMain
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
            }
        }
        petblocksCoreJavaMain {
            dependencies {
                dependsOn petblocksCoreMain
                dependsOn petblocksApiMain

                implementation 'com.google.inject:guice:4.1.0'
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
                implementation 'org.slf4j:slf4j-api:1.7.25'
            }
        }
        petblocksBukkitPluginTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
                implementation 'org.mockito:mockito-core:2.23.0'
                implementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.1'
                implementation 'org.xerial:sqlite-jdbc:3.23.1'
                implementation 'ch.vorburger.mariaDB4j:mariaDB4j:2.2.3'
                implementation 'org.spigotmc:spigot18R1:1.8.0-R1.0'
            }
        }
        petblocksCoreJavaTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }
    }
}

task shadowJarBukkitPlugin(type: ShadowJar) {
    archiveName = 'PetBlocks-Bukkit.jar'
    classifier = null
    version = null

    // Change the output folder of the plugin.
    destinationDir = file('D:/Temp/plugins')

    relocate 'kotlin', 'com.github.shynixn.petblocks.lib.kotlin'
    relocate 'org.intellij', 'com.github.shynixn.petblocks.lib.org.intellij'
    relocate 'org.jetbrains', 'com.github.shynixn.petblocks.lib.org.jetbrains'
    relocate 'org.bstats', 'com.github.shynixn.petblocks.lib.org.bstats'
    relocate 'javax.inject', 'com.github.shynixn.petblocks.lib.javax.inject'
    relocate 'org.aopalliance', 'com.github.shynixn.petblocks.lib.org.aopalliance'
    relocate 'org.slf4j', 'com.github.shynixn.petblocks.lib.org.slf4j'
    relocate 'com.google', 'com.github.shynixn.petblocks.lib.com.google'
    relocate 'com.zaxxer', 'com.github.shynixn.petblocks.lib.com.zaxxer'

    def target = kotlin.targets.petblocksBukkitPlugin
    //noinspection GroovyAssignabilityCheck
    from target.compilations.main.output
    def runtimeClasspath = target.compilations.main.runtimeDependencyFiles
    configurations = [runtimeClasspath]
}

shadowJar.finalizedBy shadowJarBukkitPlugin
